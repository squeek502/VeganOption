plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.gradle.userdev' version '7.0.57'
	id 'net.neoforged.gradle.mixin' version '7.0.57'
}

if (version == "unspecified")
	version = "0.3.0"
if (System.getenv("BUILD_NUMBER") != null)
	version += (version ? "+" : "") + "${System.getenv("BUILD_NUMBER")}"
if (System.getenv("GIT_COMMIT") != null)
	version += (version ? "." : "") + "${System.getenv("GIT_COMMIT").take(5)}"

repositories {
	mavenLocal()
	maven {
		// JEI
		url "https://maven.blamejared.com/"
	}
	maven {
		// JEI fallback
		url "https://modmaven.dev"
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url "https://maven2.bai.lol"
		content {
			includeGroup "lol.bai"
			includeGroup "mcp.mobius.waila"
		}
	}
	maven {
		url "https://maven.shedaniel.me/"
	}
}

base {
	archivesName = project.projectDir.name + "-mc1.20.2"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

runs {
	configureEach {
		systemProperty 'forge.logging.markers', 'REGISTRIES'
		systemProperty 'forge.logging.console.level', 'debug'

		modSource project.sourceSets.main
	}

	client {
		systemProperty 'forge.enabledGameTestNamespaces', 'veganoption'
	}

	server {
		systemProperty 'forge.enabledGameTestNamespaces', 'veganoption'
		programArgument '--nogui'
	}

	gameTestServer {
		systemProperty 'forge.enabledGameTestNamespaces', 'veganoption'
	}

	data {
		programArguments.addAll '--mod', 'veganoption', '--all', '--output', file('generated/resources/').getAbsolutePath(),
				'--existing', file('resources/').getAbsolutePath()
	}
}

minecraft.accessTransformers.file rootProject.file('resources/META-INF/accesstransformer.cfg')

mixin {
	config 'mixins.veganoption.json'
}

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.java.srcDirs += 'apis'
sourceSets.main.resources.srcDirs += 'resources'
sourceSets.main.resources.srcDirs += 'generated/resources'
sourceSets.test.java.srcDirs += 'tests'

def neoVersion = "20.2.86"
def neoVersionRange = "[20.2.86,20.3)"
def jeiVersion = "16.0.0.28"
def reiVersion = "13.0.685"
def jadeVersion = "4845321" // 12.1.8
def wthitVersion = "9.1.2"
def badpacketsVersion = "0.5.4"
 
dependencies {
	implementation "net.neoforged:neoforge:${neoVersion}"
	implementation "curse.maven:jade-324717:${jadeVersion}"
	// We need the default implementations in REI, rather than just the API
	implementation "me.shedaniel:RoughlyEnoughItems-neoforge:${reiVersion}"

	compileOnly "mcp.mobius.waila:wthit-api:neo-${wthitVersion}"

	runtimeOnly "mcp.mobius.waila:wthit:neo-${wthitVersion}"
	runtimeOnly "lol.bai:badpackets:neo-${badpacketsVersion}"

//	Awaiting a NeoForge version...
//	compileOnly "mezz.jei:jei-1.20.2-common-api:${jeiVersion}"
//	compileOnly "mezz.jei:jei-1.20.2-forge-api:${jeiVersion}"
//	runtimeOnly "mezz.jei:jei-1.20.2-forge:${jeiVersion}"
}

tasks.withType(ProcessResources).configureEach {
	var replaceProperties = [
			minecraft_version: "1.20.2", minecraft_version_range: "1.20.2",
			neo_version: neoVersion, neo_version_range: neoVersionRange,
			loader_version_range: "[1,)",
			mod_id: "veganoption",
			mod_name: "The Vegan Option",
			mod_license: "Public Domain",
			mod_version: project.version,
			mod_authors: "squeek502, SatanicSanta",
			mod_description: "Adds vegan alternatives for the various animal products used in Minecraft.",
	]
	inputs.properties replaceProperties

	filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
		expand replaceProperties + [project: project]
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}